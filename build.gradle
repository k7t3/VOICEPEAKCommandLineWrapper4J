plugins {
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java-library'

    group = 'io.github.k7t3'
    version = voicepeakcw4jVersion

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaBasePlugin).configureEach {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(javaVersion)
            }

            withJavadocJar()
            withSourcesJar()
        }

        compileJava {
            options.encoding = 'UTF-8'
        }

        javadoc {
            options.encoding = 'UTF-8'
            options.windowTitle("$project.name")
            options.addBooleanOption('html5', true)
        }
    }

    plugins.withType(MavenPublishPlugin).configureEach {
        publishing {
            publications {
                maven(MavenPublication) {
                    artifactId = "$project.name"

                    pom {
                        name = "$project.name"
                        description = 'This library is a command line wrapper for the VOICEPEAK published by AHS Co.Ltd.'
                        url = 'https://github.com/k7t3/VOICEPEAKCommandLineWrapper4J'

                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'k7t3'
                                name = 'k7t3'
                                email = 'k7t3annnn@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/k7t3/VOICEPEAKCommandLineWrapper4J'
                            developerConnection = 'scm:git:git@github.com:k7t3/VOICEPEAKCommandLineWrapper4J.git'
                            url = 'https://github.com/k7t3/VOICEPEAKCommandLineWrapper4J'
                        }
                    }

                    from components.java
                }
            }

            repositories {
                maven {
                    def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                    credentials {
                        username = System.getenv("MAVEN_USERNAME")
                        password = System.getenv("MAVEN_PASSWORD")
                    }
                }
            }
        }
    }

    plugins.withType(SigningPlugin).configureEach {
        signing {
            def signingKey = System.getenv("SIGNING_KEY")
            def signingPassword = System.getenv("SIGNING_PASSWORD")
            useInMemoryPgpKeys(signingKey, signingPassword)
            required { gradle.taskGraph.hasTask("publishMavenJavaPublicationToMavenRepository") }
            //sign publishing.publications.mavenJava
            sign publishing.publications
        }
    }
}